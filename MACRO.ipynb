{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "MACRO\n",
    "=================="
   ]
  },
  {
   "cell_type": "raw",
   "metadata": {},
   "source": [
    "\n",
    "* option 1: macro quote the apostrophe ;\n",
    "%let q=%str(%');\n",
    "%let name=Jane;\n",
    "\n",
    "* this does not work! ;\n",
    "data test;\n",
    "   set class;\n",
    "   where name=&q&name&q;  \n",
    "run;\n",
    "\n",
    "* but this does ;\n",
    "data test;\n",
    "   set class;\n",
    "   where name=%unquote(&q&name&q);  \n",
    "run;\n",
    "\n",
    "\n",
    "* but if you are doing explicit passthrough, ;\n",
    "* SAS unquotes the value for you ;\n",
    "proc sql;\n",
    "   connect using tmp;\n",
    "   create table test as\n",
    "   select * from connection to tmp (\n",
    "      select * from tmp.class where name=&q&name&q\n",
    "   );\n",
    "quit;\n",
    "\n",
    "\n",
    "* option 2: use the %bquote function ;\n",
    "* same characteristics as before ;\n",
    "\n",
    "* this does not work! ;\n",
    "data test;\n",
    "   set class;\n",
    "   where name=%bquote('&name');\n",
    "run;\n",
    "\n",
    "* but this does ;\n",
    "data test;\n",
    "   set class;\n",
    "   where name=%unquote(%bquote('&name'));\n",
    "run;\n",
    "\n",
    "* but if you are doing explicit passthrough, ;\n",
    "* SAS unquotes the value for you ;\n",
    "proc sql;\n",
    "   connect using tmp;\n",
    "   create table test as\n",
    "   select * from connection to tmp (\n",
    "      select * from tmp.class where name=%bquote('&name')\n",
    "   );\n",
    "quit;\n",
    "\n",
    " \n",
    "* option 3: use the %tslit macro ;\n",
    "* Note: works in SAS 9.3 ;\n",
    "* I hate this hideous name, and the symbolgen output is \"messy\"... ;\n",
    "* but it works ;\n",
    "\n",
    "* See http://blogs.sas.com/content/sgf/2014/08/15/macro-quoting-made-easy/ ;\n",
    "* and https://communities.sas.com/t5/SAS-Communities-Library/Not-All-Macro-Language-Elements-Are-Supported-by-the-Macro/ta-p/223904 ;\n",
    "\n",
    "options symbolgen;\n",
    "data test;\n",
    "   set class;\n",
    "   where name=%tslit(&name);\n",
    "run;\n",
    "\n",
    "proc sql;\n",
    "   connect using tmp;\n",
    "   create table test as\n",
    "   select * from connection to tmp (\n",
    "      select * from tmp.class where name=%tslit(&name)\n",
    "   );\n",
    "quit;\n",
    "\n",
    "\n",
    "\n",
    "* option 4: use %squote macro ;\n",
    "* See https://communities.sas.com/t5/SAS-Communities-Library/Not-All-Macro-Language-Elements-Are-Supported-by-the-Macro/ta-p/223904 ;\n",
    "* and https://github.com/scottbass/SAS/blob/master/Macro/squote.sas ;\n",
    "data test;\n",
    "   set class;\n",
    "   where name=%squote(&name);\n",
    "run;\n",
    "\n",
    "proc sql;\n",
    "   connect using tmp;\n",
    "   create table test as\n",
    "   select * from connection to tmp (\n",
    "      select * from tmp.class where name=%squote(&name)\n",
    "   );\n",
    "quit;\n",
    "\n",
    "options nosymbolgen;\n"
   ]
  },
  {
   "cell_type": "raw",
   "metadata": {},
   "source": [
    "\n",
    "24         * you no longer need to specify an upper bound ;\n",
    "25         * for creating multiple macro variables in proc sql ;\n",
    "26         * Note: works in SAS 9.3 ;\n",
    "27         proc sql noprint;\n",
    "28            select distinct name\n",
    "29            into :name1-\n",
    "30            from sashelp.class;\n",
    "31         quit;\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "options ls=max;  * so the SAS log lines do not wrap ;\n",
    "options mprint;  * print macro generated code ;\n",
    "\n",
    "* create source data ;\n",
    "data source (index=(acct));\n",
    "  length acct parent $3 amount 8;\n",
    "  input acct parent amount;\n",
    "  datalines;\n",
    "A1  .   10\n",
    "A2  A1  20\n",
    "A3  A2  30\n",
    "A4  A2  20\n",
    "A5  A1  10\n",
    "A6  A5  10\n",
    "A7  .   10\n",
    "A8  A7  20\n",
    "A9  A7  30\n",
    "A10 A9  20\n",
    ";\n",
    "run;\n",
    "\n",
    "%macro code;\n",
    "* option 1: index key lookup to derive top-level parent ;\n",
    "\n",
    "* use recursion to get the top level parent ;\n",
    "/* Note: This is my first version...I prefer the second approach\n",
    "data option1;\n",
    "  set source;\n",
    "  temp=acct;\n",
    "  do while (1);\n",
    "    if missing(parent) then do;\n",
    "      parent=acct;\n",
    "      acct=temp;\n",
    "      leave;\n",
    "    end;\n",
    "    acct=parent;\n",
    "    set source (drop=amount) key=acct / unique;\n",
    "  end;\n",
    "  _error_=0;\n",
    "  drop temp;\n",
    "run;\n",
    "*/\n",
    "\n",
    "data option1;\n",
    "  set source;\n",
    "  _acct=acct;\n",
    "  _parent=parent;\n",
    "  do while (not missing(parent));\n",
    "    acct=parent;\n",
    "    set source (drop=amount) key=acct / unique;\n",
    "  end;\n",
    "  toplevel=acct;\n",
    "  acct=_acct;\n",
    "  parent=_parent;\n",
    "  _error_=0;\n",
    "  drop _:;\n",
    "run;\n",
    "\n",
    "proc summary data=option1 nway noprint;\n",
    "  class toplevel;\n",
    "  var amount;\n",
    "  output out=sum1 sum=;\n",
    "run;"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SAS",
   "language": "sas",
   "name": "sas"
  },
  "language_info": {
   "codemirror_mode": "sas",
   "file_extension": ".sas",
   "mimetype": "text/x-sas",
   "name": "sas"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 0
}
